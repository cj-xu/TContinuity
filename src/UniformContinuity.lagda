
  =========================================================
  =                                                       =
  =  Â§5.1. Uniform continuity of T-definable functionals  =
  =                                                       =
  =========================================================

     Chuangjie Xu, 2018


Using our syntactic method with a predicate for uniform continuity, we
show that if f : (â„• â†’ â„•) â†’ â„• is T-definable then its restriction
fâ†‘ğŸšá´º : (â„• â†’ ğŸš) â†’ â„• is uniformly continuous.  Our method seems related
to the sheaf model introduced in [Xu15].  For instance, the proof of
UC(Î©) is essentially same as the proof of the Yoneda Lemma in the model,
which plays a key role in the construction of the Fan functional.


Reference.

[Xu15]  C. Xu.  A continuous computational interpretation of type theories.
        PhD thesis, School of Computer Science, University of Birmingham, 2015.

\begin{code}

module UniformContinuity where

open import Preliminaries
open import T

\end{code}

â—¼ A mini library

\begin{code}

--
-- Booleans
--
data ğŸš : Set where
 ğŸ ğŸ : ğŸš

Lemma[bâ‰¡0âˆ¨bâ‰¡1] : (b : ğŸš) â†’ (b â‰¡ ğŸ) âˆ¨ (b â‰¡ ğŸ)
Lemma[bâ‰¡0âˆ¨bâ‰¡1] ğŸ = inl refl
Lemma[bâ‰¡0âˆ¨bâ‰¡1] ğŸ = inr refl

--
-- The Cantor space/type
--
ğŸšá´º : Set
ğŸšá´º = ğŸš á´º

ğŸÊ· : ğŸšá´º
ğŸÊ· = ğŸ Ê·

ğŸštoâ„• : ğŸš â†’ â„•
ğŸštoâ„• ğŸ = 0
ğŸštoâ„• ğŸ = 1

ğŸšá´ºtoâ„•á´º : ğŸšá´º â†’ â„•á´º
ğŸšá´ºtoâ„•á´º Î± i = ğŸštoâ„• (Î± i)

--
-- Restriction of functions from the Baire space to the Cantor space
--
_â†¾ğŸšá´º : {A : Set} â†’ (â„•á´º â†’ A) â†’ ğŸšá´º â†’ A
(f â†¾ğŸšá´º) Î± = f (ğŸšá´ºtoâ„•á´º Î±)

\end{code}

â–  Continuity of functions â„•á´º â†’ â„•

\begin{code}

uniformly-continuous : (ğŸšá´º â†’ â„•) â†’ Set
uniformly-continuous f = Î£ \(m : â„•) â†’ (Î± Î² : ğŸšá´º) â†’ Î± â‰¡[ m ] Î² â†’ f Î± â‰¡ f Î²

uc-extensional : {f g : ğŸšá´º â†’ â„•} â†’ (âˆ€ Î± â†’ f Î± â‰¡ g Î±)
               â†’ uniformly-continuous f â†’ uniformly-continuous g
uc-extensional {f} {g} e (m , pá¶ ) = m , páµ
 where
  páµ : (Î± Î² : ğŸšá´º) â†’ Î± â‰¡[ m ] Î² â†’ g Î± â‰¡ g Î²
  páµ Î± Î² em = trans (sym (e Î±)) (trans (pá¶  Î± Î² em) (e Î²))

--
-- Lemma: Any uniformly continuous function ğŸšá´º â†’ â„• has a maximun image.
--
-- Proof. By induction on the moduli of uniform continuity.
--
uc-max-image : {f : ğŸšá´º â†’ â„•} (m : â„•) â†’ ((Î± Î² : ğŸšá´º) â†’ Î± â‰¡[ m ] Î² â†’ f Î± â‰¡ f Î²)
             â†’ Î£ \(n : â„•) â†’ (Î± : ğŸšá´º) â†’ f Î± â‰¤ n
uc-max-image {f}  0       p0  = f ğŸÊ· , Î» Î± â†’ â‰¤refl' (p0 Î± ğŸÊ· â‰¡[zero])
uc-max-image {f} (succ m) psm = n , prf
 where
  pmâ‚€ : (Î± Î² : ğŸšá´º) â†’ Î± â‰¡[ m ] Î² â†’ f (ğŸ âœ¶ Î±) â‰¡ f (ğŸ âœ¶ Î²)
  pmâ‚€ Î± Î² em = psm (ğŸ âœ¶ Î±) (ğŸ âœ¶ Î²) (â‰¡[succ] refl em)
  IHâ‚€ : Î£ \(n : â„•) â†’ (Î± : ğŸšá´º) â†’ f (ğŸ âœ¶ Î±) â‰¤ n
  IHâ‚€ = uc-max-image m pmâ‚€
  pmâ‚ : (Î± Î² : ğŸšá´º) â†’ Î± â‰¡[ m ] Î² â†’ f (ğŸ âœ¶ Î±) â‰¡ f (ğŸ âœ¶ Î²)
  pmâ‚ Î± Î² em = psm (ğŸ âœ¶ Î±) (ğŸ âœ¶ Î²) (â‰¡[succ] refl em)
  IHâ‚ : Î£ \(n : â„•) â†’ (Î± : ğŸšá´º) â†’ f (ğŸ âœ¶ Î±) â‰¤ n
  IHâ‚ = uc-max-image m pmâ‚
  nâ‚€ nâ‚ n : â„•
  nâ‚€ = prâ‚ IHâ‚€
  nâ‚ = prâ‚ IHâ‚
  n  = max nâ‚€ nâ‚
  prf : (Î± : ğŸšá´º) â†’ f Î± â‰¤ n
  prf Î± with Lemma[bâ‰¡0âˆ¨bâ‰¡1] (head Î±)
  prf Î± | inl Î±0=0 = transport (Î» x â†’ x â‰¤ n) claimâ‚€ claimâ‚‚
   where
    esm : (ğŸ âœ¶ tail Î±) â‰¡[ succ m ] Î±
    esm = â‰¡[succ] (sym Î±0=0) â‰¡[]-refl
    claimâ‚€ : f (ğŸ âœ¶ tail Î±) â‰¡ f Î±
    claimâ‚€ = psm (ğŸ âœ¶ tail Î±) Î± esm
    claimâ‚ : f (ğŸ âœ¶ tail Î±) â‰¤ nâ‚€
    claimâ‚ = prâ‚‚ IHâ‚€ (tail Î±)
    claimâ‚‚ : f (ğŸ âœ¶ tail Î±) â‰¤ n
    claimâ‚‚ = â‰¤trans claimâ‚ (max-specâ‚€ nâ‚€ nâ‚)
  prf Î± | inr Î±0=1 = transport (Î» x â†’ x â‰¤ n) claimâ‚€ claimâ‚‚
   where
    esm : (ğŸ âœ¶ tail Î±) â‰¡[ succ m ] Î±
    esm = â‰¡[succ] (sym Î±0=1) â‰¡[]-refl
    claimâ‚€ : f (ğŸ âœ¶ tail Î±) â‰¡ f Î±
    claimâ‚€ = psm (ğŸ âœ¶ tail Î±) Î± esm
    claimâ‚ : f (ğŸ âœ¶ tail Î±) â‰¤ nâ‚
    claimâ‚ = prâ‚‚ IHâ‚ (tail Î±)
    claimâ‚‚ : f (ğŸ âœ¶ tail Î±) â‰¤ n
    claimâ‚‚ = â‰¤trans claimâ‚ (max-specâ‚ nâ‚€ nâ‚)

\end{code}

â–  A uniform-continuity predicate UC âŠ† Ïáµ‡

\begin{code}

UC : (Ï : Ty) â†’ âŸ¦ âŸ¨ Ï âŸ©áµ‡ âŸ§Ê¸ â†’ Set
UC  Î¹      f = uniformly-continuous (f â†¾ğŸšá´º)
UC (Ïƒ â‡¾ Ï„) h = (x : âŸ¦ âŸ¨ Ïƒ âŸ©áµ‡ âŸ§Ê¸) â†’ UC Ïƒ x â†’ UC Ï„ (h x)

--
-- The Kleisli extension preserves the predicate.
--
UC[KEá¶¥] : (Ï : Ty)
        â†’ {g : â„• â†’ âŸ¦ âŸ¨ Ï âŸ©áµ‡ âŸ§Ê¸} â†’ ((i : â„•) â†’ UC Ï (g i))
        â†’ {Î“ : Cxt} {Î³ : âŸ¦ Î“ âŸ§Ë£}
        â†’ UC (Î¹ â‡¾ Ï) (âŸ¦ KEá¶¥ Ï âŸ§áµ Î³ g)
UC[KEá¶¥] Î¹ {g} ucg {_} {_} f (m , pá¶ ) = k , prf
 where
  Max : (â„• â†’ â„•) â†’ â„• â†’ â„•
  Max Ï†  0       = Ï† 0
  Max Ï† (succ i) = max (Max Ï† i) (Ï† (succ i))
  lemma : (f : â„• â†’ â„•) â†’ (âˆ€ i â†’ f i â‰¤ f (succ i)) â†’ (i j : â„•) â†’ i â‰¤ j â†’ f i â‰¤ f j
  lemma f up  0        0       â‰¤zero = â‰¤refl
  lemma f up  0       (succ j) â‰¤zero = â‰¤trans (lemma f up 0 j â‰¤zero) (up j)
  lemma f up (succ i)  0       ()
  lemma f up (succ i) (succ j) (â‰¤succ r) = lemma (Î» x â†’ f (succ x)) (Î» x â†’ up (succ x)) i j r
  Max-specâ‚ : {Ï† : â„• â†’ â„•} {i j : â„•} â†’ i â‰¤ j â†’ Max Ï† i â‰¤ Max Ï† j
  Max-specâ‚ {Ï†} r = lemma (Max Ï†) (Î» i â†’ max-specâ‚€ (Max Ï† i) _) _ _ r
  Max-specâ‚€ : {Ï† : â„• â†’ â„•} (i : â„•) â†’ Ï† i â‰¤ Max Ï† i
  Max-specâ‚€      0       = â‰¤refl
  Max-specâ‚€ {Ï†} (succ i) = max-specâ‚ (Max Ï† i) (Ï† (succ i))
  Max-spec : {Ï† : â„• â†’ â„•} {i j : â„•} â†’ i â‰¤ j â†’ Ï† i â‰¤ Max Ï† j
  Max-spec {Ï†} {i} r = â‰¤trans (Max-specâ‚€ i) (Max-specâ‚ r)
  Ï† : â„• â†’ â„•
  Ï† i = prâ‚ (ucg i)
  l : â„•
  l = prâ‚ (uc-max-image m pá¶ )
  n : â„•
  n = Max Ï† l
  k : â„•
  k = max m n
  k-spec : (Î± : ğŸšá´º) â†’ prâ‚ (ucg (f (ğŸšá´ºtoâ„•á´º Î±))) â‰¤ k
  k-spec Î± = â‰¤trans (Max-spec (prâ‚‚ (uc-max-image m pá¶ ) Î±)) (max-specâ‚ m n)
  prf : (Î± Î² : ğŸšá´º) â†’ Î± â‰¡[ k ] Î² â†’ g (f (ğŸšá´ºtoâ„•á´º Î±)) (ğŸšá´ºtoâ„•á´º Î±) â‰¡ g (f (ğŸšá´ºtoâ„•á´º Î²)) (ğŸšá´ºtoâ„•á´º Î²)
  prf Î± Î² ek = trans eâ‚‚ eâ‚
   where
    eâ‚€ : f (ğŸšá´ºtoâ„•á´º Î±) â‰¡ f (ğŸšá´ºtoâ„•á´º Î²)
    eâ‚€ = pá¶  Î± Î² (â‰¡[â‰¤] ek (max-specâ‚€ m n))
    eâ‚ : g (f (ğŸšá´ºtoâ„•á´º Î±)) (ğŸšá´ºtoâ„•á´º Î²) â‰¡ g (f (ğŸšá´ºtoâ„•á´º Î²)) (ğŸšá´ºtoâ„•á´º Î²)
    eâ‚ = ap (Î» i â†’ g i (ğŸšá´ºtoâ„•á´º Î²)) eâ‚€
    eâ‚‚ : g (f (ğŸšá´ºtoâ„•á´º Î±)) (ğŸšá´ºtoâ„•á´º Î±) â‰¡ g (f (ğŸšá´ºtoâ„•á´º Î±)) (ğŸšá´ºtoâ„•á´º Î²)
    eâ‚‚ = prâ‚‚ (ucg (f (ğŸšá´ºtoâ„•á´º Î±))) Î± Î² (â‰¡[â‰¤] ek (k-spec Î±))
UC[KEá¶¥] (_ â‡¾ Ï) cg f cf = Î» a ca â†’ UC[KEá¶¥] Ï (Î» i â†’ cg i a ca) f cf

--
-- Extending the predicate to contexts
--
UCË£ : {Î“ : Cxt} â†’ âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£ â†’ Set
UCË£ {Îµ}      â‹†      = ğŸ™
UCË£ {Î“ â‚Š Ï} (Î³ , a) = UCË£ Î³ Ã— UC Ï a

--
-- Variables satisfy the predicate.
--
UC[Var] : {Î“ : Cxt} {Ï : Ty} {Î³ : âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£}
        â†’ UCË£ Î³ â†’ (i : âˆ¥ Ï âˆˆ Î“ âˆ¥) â†’ UC Ï (Î³ â‚ âŸ¨ i âŸ©áµ› â‚)
UC[Var] {Îµ}      _      ()
UC[Var] {Î“ â‚Š Ï} (Î´ , Î¸)  zero    = Î¸
UC[Var] {Î“ â‚Š Ï} (Î´ , Î¸) (succ i) = UC[Var] Î´ i

--
-- The b-translation of any T term satisfies the predicate.
--
Lemma[UC] : {Î“ : Cxt} {Ï : Ty} (t : T Î“ Ï)
          â†’ {Î³ : âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£} â†’ UCË£ Î³ â†’ UC Ï (âŸ¦ t áµ‡ âŸ§áµ Î³)
Lemma[UC] (Var i) Î´ = UC[Var] Î´ i
Lemma[UC] (Lam t) Î´ = Î» _ Î¸ â†’ Lemma[UC] t (Î´ , Î¸)
Lemma[UC] (f Â· a) Î´ = Lemma[UC] f Î´ _ (Lemma[UC] a Î´)
Lemma[UC] Zero _ = (0 , Î» _ _ _ â†’ refl) -- Î» Î± â†’ (0 , Î» _ _ â†’ refl)
Lemma[UC] Succ _ f (m , pá¶ ) = (m , Î» Î± Î² em â†’ ap succ (pá¶  Î± Î² em))
Lemma[UC] (Rec {Ï}) _ a uca f ucf = UC[KEá¶¥] Ï cg
 where
  cg : (i : â„•) â†’ UC Ï _
  cg  0       = uca
  cg (succ i) = ucf _ ((0 , Î» _ _ _ â†’ refl)) _ (cg i) -- ucf _ (0 , (Î» _ _ _ â†’ refl)) (cg i)

--
-- The generic element Î© satisfies the predicate.
--
UC[Î©] : UC (Î¹ â‡¾ Î¹) âŸ¦ Î© âŸ§
UC[Î©] f (m , pá¶ ) = k , prf
 where
  n : â„•
  n = succ (prâ‚ (uc-max-image m pá¶ ))
  k : â„•
  k = max m n
  k-spec : (Î± : ğŸšá´º) â†’ succ ((f â†¾ğŸšá´º) Î±) â‰¤ k
  k-spec Î± = â‰¤trans (â‰¤succ (prâ‚‚ (uc-max-image m pá¶ ) Î±)) (max-specâ‚ m n)
  prf : (Î± Î² : ğŸšá´º) â†’ Î± â‰¡[ k ] Î² â†’ (ğŸšá´ºtoâ„•á´º Î±) (f (ğŸšá´ºtoâ„•á´º Î±)) â‰¡ (ğŸšá´ºtoâ„•á´º Î²) (f (ğŸšá´ºtoâ„•á´º Î²))
  prf Î± Î² ek = trans eâ‚‚ eâ‚
   where
    eâ‚€ : f (ğŸšá´ºtoâ„•á´º Î±) â‰¡ f (ğŸšá´ºtoâ„•á´º Î²)
    eâ‚€ = pá¶  Î± Î² (â‰¡[â‰¤] ek (max-specâ‚€ m n))
    eâ‚ : (ğŸšá´ºtoâ„•á´º Î²) (f (ğŸšá´ºtoâ„•á´º Î±)) â‰¡ (ğŸšá´ºtoâ„•á´º Î²) (f (ğŸšá´ºtoâ„•á´º Î²))
    eâ‚ = ap (ğŸšá´ºtoâ„•á´º Î²) eâ‚€
    eâ‚‚ : (ğŸšá´ºtoâ„•á´º Î±) (f (ğŸšá´ºtoâ„•á´º Î±)) â‰¡ (ğŸšá´ºtoâ„•á´º Î²) (f (ğŸšá´ºtoâ„•á´º Î±))
    eâ‚‚ = ap ğŸštoâ„• (â‰¡[]-< ek (k-spec Î±))

\end{code}

â–  Theorem: All T-definable functions â„•á´º â†’ â„• are continuous.

\begin{code}

Theorem[TUC] : (f : â„•á´º â†’ â„•) â†’ T-definable f â†’ uniformly-continuous (f â†¾ğŸšá´º)
Theorem[TUC] f (t , refl) = ucá¶ 
 where
  claimâ‚€ : (Î± : ğŸšá´º) â†’ (âŸ¦ t áµ‡ Â· Î© âŸ§ â†¾ğŸšá´º) Î± â‰¡ (âŸ¦ t âŸ§ â†¾ğŸšá´º) Î±
  claimâ‚€ Î± = Theorem[R] t (ğŸšá´ºtoâ„•á´º Î±)
  claimâ‚ : uniformly-continuous (âŸ¦ t áµ‡ Â· Î© âŸ§ â†¾ğŸšá´º)
  claimâ‚ = Lemma[UC] t â‹† âŸ¦ Î© âŸ§ UC[Î©]
  ucá¶  : uniformly-continuous (f â†¾ğŸšá´º)
  ucá¶  = uc-extensional claimâ‚€ claimâ‚

\end{code}


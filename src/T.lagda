
  ================================================
  =                                              =
  =  Â§2. GÃ¶del's system T and the b-translation  =
  =                                              =
  ================================================

     Chuangjie Xu, 2018


We define (the term language of) GÃ¶del's system T in a lambda-calculus
form with de Bruijn indices instead of variables.  Then we embed T
into Agda by providing a standard interpretation of T in Agda.  As a
preliminary step of the syntactic method, we introduce a (generalized)
b-translation of system T into itself.  This is inspired by Oliva and
Steila's proof of the bar recursion closure theorem [OS18].  We relate
terms and their b-translation via a parametrized logical relation that
was introduced in one version of the Agda implementation of [Esc13].


Reference.

[Esc13] M. H. EscardÃ³.  Continuity of GÃ¶delâ€™s system T functionals via
        effectful forcing.  MFPSâ€™2013.  Electronic Notes in
        Theoretical Computer Science, 298:119â€“141, 2013.

[OS18]  P. Oliva and S. Steila.  A direct proof of Schwichtenbergâ€™s bar
        recursion closure theorem.  The Journal of Symbolic Logic,
        83(1):70â€“83, 2018.

\begin{code}

module T where

open import Preliminaries

\end{code}

â–  GÃ¶del's system T

Here we work with the lambda-calculus version of system T, and with de
Bruijn indices instead of variables.

\begin{code}

infixr 30 _â‡¾_
infixl 20 _â‚Š_
infixl 20 _Â·_

--
-- finite types
--
data Ty : Set where
 Î¹   : Ty
 _â‡¾_ : Ty â†’ Ty â†’ Ty

--
-- contexts as finite lists of types
--
data Cxt : Set where
 Îµ   : Cxt
 _â‚Š_ : Cxt â†’ Ty â†’ Cxt

--
-- indices of types (i.e. variables in context)
--
data âˆ¥_âˆˆ_âˆ¥ (Ïƒ : Ty) : Cxt â†’ Set where
 zero : {Î“ : Cxt} â†’ âˆ¥ Ïƒ âˆˆ (Î“ â‚Š Ïƒ) âˆ¥
 succ : {Ï„ : Ty} {Î“ : Cxt} â†’ âˆ¥ Ïƒ âˆˆ Î“ âˆ¥ â†’ âˆ¥ Ïƒ âˆˆ Î“ â‚Š Ï„ âˆ¥

--
-- terms
--
data T (Î“ : Cxt) : Ty â†’ Set where
 Var  : {Ïƒ : Ty} â†’ âˆ¥ Ïƒ âˆˆ Î“ âˆ¥ â†’ T Î“ Ïƒ
 Lam  : {Ïƒ Ï„ : Ty} â†’ T (Î“ â‚Š Ïƒ) Ï„ â†’ T Î“ (Ïƒ â‡¾ Ï„)
 _Â·_  : {Ïƒ Ï„ : Ty} â†’ T Î“ (Ïƒ â‡¾ Ï„) â†’ T Î“ Ïƒ â†’ T Î“ Ï„
 Zero : T Î“ Î¹
 Succ : T Î“ (Î¹ â‡¾ Î¹)
 Rec  : {Ïƒ : Ty} â†’ T Î“ (Ïƒ â‡¾ (Î¹ â‡¾ Ïƒ â‡¾ Ïƒ) â‡¾ Î¹ â‡¾ Ïƒ)

\end{code}

â–  Some auxiliaries

\begin{code}

Î½â‚€ : {Î“ : Cxt} {Ï : Ty} â†’ T (Î“ â‚Š Ï) Ï
Î½â‚€ = Var zero

Î½â‚ : {Î“ : Cxt} {Ï Ïƒ : Ty} â†’ T (Î“ â‚Š Ï â‚Š Ïƒ) Ï
Î½â‚ = Var (succ zero)

Î½â‚‚ : {Î“ : Cxt} {Ï Ïƒâ‚€ Ïƒâ‚ : Ty} â†’ T (Î“ â‚Š Ï â‚Š Ïƒâ‚€ â‚Š Ïƒâ‚) Ï
Î½â‚‚ = Var (succ (succ zero))

Î½â‚ƒ : {Î“ : Cxt} {Ï Ïƒâ‚€ Ïƒâ‚ Ïƒâ‚‚ : Ty} â†’ T (Î“ â‚Š Ï â‚Š Ïƒâ‚€ â‚Š Ïƒâ‚ â‚Š Ïƒâ‚‚) Ï
Î½â‚ƒ = Var (succ (succ (succ zero)))

Num : â„• â†’ {Î“ : Cxt} â†’ T Î“ Î¹
Num  0       = Zero
Num (succ k) = Succ Â· Num k

Plus : {Î“ : Cxt} â†’ T Î“ Î¹ â†’ T Î“ Î¹ â†’ T Î“ Î¹
Plus N M = Rec Â· N Â· Lam Succ Â· M

\end{code}

â–  The standard interpretation of T into Agda

\begin{code}

âŸ¦_âŸ§Ê¸ : Ty â†’ Set
âŸ¦ Î¹ âŸ§Ê¸     = â„•
âŸ¦ Ïƒ â‡¾ Ï„ âŸ§Ê¸ = âŸ¦ Ïƒ âŸ§Ê¸ â†’ âŸ¦ Ï„ âŸ§Ê¸

âŸ¦_âŸ§Ë£ : Cxt â†’ Set
âŸ¦ Îµ âŸ§Ë£     = ğŸ™
âŸ¦ Î“ â‚Š Ï âŸ§Ë£ = âŸ¦ Î“ âŸ§Ë£ Ã— âŸ¦ Ï âŸ§Ê¸

_â‚_â‚ : {Î“ : Cxt} {Ï : Ty} â†’ âŸ¦ Î“ âŸ§Ë£ â†’ âˆ¥ Ï âˆˆ Î“ âˆ¥ â†’ âŸ¦ Ï âŸ§Ê¸
(_ , a) â‚ zero â‚   = a
(Î³ , _) â‚ succ i â‚ = Î³ â‚ i â‚

âŸ¦_âŸ§áµ : {Î“ : Cxt} {Ïƒ : Ty} â†’ T Î“ Ïƒ â†’ âŸ¦ Î“ âŸ§Ë£ â†’ âŸ¦ Ïƒ âŸ§Ê¸
âŸ¦ Var i âŸ§áµ Î³ = Î³ â‚ i â‚
âŸ¦ Lam t âŸ§áµ Î³ = Î» a â†’ âŸ¦ t âŸ§áµ (Î³ , a)
âŸ¦ f Â· a âŸ§áµ Î³ = âŸ¦ f âŸ§áµ Î³ (âŸ¦ a âŸ§áµ Î³)
âŸ¦ Zero âŸ§áµ  _ = 0
âŸ¦ Succ âŸ§áµ  _ = succ
âŸ¦ Rec âŸ§áµ   _ = rec

âŸ¦_âŸ§ : {Ï : Ty} â†’ T Îµ Ï â†’ âŸ¦ Ï âŸ§Ê¸
âŸ¦ t âŸ§ = âŸ¦ t âŸ§áµ â‹†

--
-- An (Agda) element a is "T-definable" if one can find a closed term
-- in T whose standard interpretation is a.
--
T-definable : {Ï : Ty} â†’ âŸ¦ Ï âŸ§Ê¸ â†’ Set
T-definable {Ï} a = Î£ \(t : T Îµ Ï) â†’ âŸ¦ t âŸ§ â‰¡ a

\end{code}

â–  (Generalized) b-translation

Suppose the goal is to use the usual syntactic method to prove certain
property of functions â–¡ â†’ â„•, where â–¡ is a finite type.  The first step
is to perform a syntactic translation from system T to itself where
the base type â„• is translated to â–¡ â†’ â„•, so that the base case of the
predicate to work with is the targeting property of functions â–¡ â†’ â„•.
Then we can achieve the goal by showing that the translation of any
term in T satisfies the predicate by induction on terms.

\begin{code}

module Translation (â–¡ : Ty) where

 âŸ¨_âŸ©áµ‡ : Ty â†’ Ty
 âŸ¨ Î¹ âŸ©áµ‡     = â–¡ â‡¾ Î¹
 âŸ¨ Ïƒ â‡¾ Ï„ âŸ©áµ‡ = âŸ¨ Ïƒ âŸ©áµ‡ â‡¾ âŸ¨ Ï„ âŸ©áµ‡

 âŸª_âŸ«áµ‡ : Cxt â†’ Cxt
 âŸª Îµ âŸ«áµ‡     = Îµ
 âŸª Î“ â‚Š Ï âŸ«áµ‡ = âŸª Î“ âŸ«áµ‡ â‚Š âŸ¨ Ï âŸ©áµ‡

 âŸ¨_âŸ©áµ› : {Î“ : Cxt} {Ï : Ty} â†’ âˆ¥ Ï âˆˆ Î“ âˆ¥ â†’ âˆ¥ âŸ¨ Ï âŸ©áµ‡ âˆˆ âŸª Î“ âŸ«áµ‡ âˆ¥
 âŸ¨ zero âŸ©áµ›   = zero
 âŸ¨ succ i âŸ©áµ› = succ âŸ¨ i âŸ©áµ›

 KEá¶¥ : {Î“ : Cxt} (Ï : Ty)
     â†’ T Î“ ((Î¹ â‡¾ âŸ¨ Ï âŸ©áµ‡) â‡¾ âŸ¨ Î¹ âŸ©áµ‡ â‡¾ âŸ¨ Ï âŸ©áµ‡)
 KEá¶¥  Î¹      = Lam (Lam (Lam (Î½â‚‚ Â· (Î½â‚ Â· Î½â‚€) Â· Î½â‚€)))
 KEá¶¥ (_ â‡¾ Ï) = Lam (Lam (Lam (KEá¶¥ Ï Â· Lam (Î½â‚ƒ Â· Î½â‚€ Â· Î½â‚) Â· Î½â‚)))

 infix 60 _áµ‡

 _áµ‡ : {Î“ : Cxt} {Ï : Ty} â†’ T Î“ Ï â†’ T âŸª Î“ âŸ«áµ‡ âŸ¨ Ï âŸ©áµ‡
 (Var i) áµ‡   = Var âŸ¨ i âŸ©áµ›
 (Lam t) áµ‡   = Lam (t áµ‡)
 (f Â· a) áµ‡   = f áµ‡ Â· a áµ‡
 Zero áµ‡      = Lam Zero  -- Î»Î±.0
 Succ áµ‡      = Lam (Lam (Succ Â· (Î½â‚ Â· Î½â‚€)))  -- Î»fx.succ(fx)
 (Rec {Ï}) áµ‡ = Lam (Lam (KEá¶¥ Ï Â· (Rec Â· Î½â‚ Â· Lam (Î½â‚ Â· Lam Î½â‚))))  -- Î»af.KE(rec(a,(Î»k.f(Î»Î±.k))))

\end{code}

â–  Relating terms and their b-translations

Following EscardÃ³ [Esc13], we use a parametrized logical relation that
is introduced in one version of the Agda implementation of [Esc13] to
relate (the standard interpretations of) terms and their b-translations.

\begin{code}

 R : {Ï : Ty} â†’ âŸ¦ â–¡ âŸ§Ê¸ â†’ âŸ¦ âŸ¨ Ï âŸ©áµ‡ âŸ§Ê¸ â†’ âŸ¦ Ï âŸ§Ê¸ â†’ Set
 R {Î¹    } Î± f n = f Î± â‰¡ n
 R {Ïƒ â‡¾ Ï„} Î± f g = {x : âŸ¦ âŸ¨ Ïƒ âŸ©áµ‡ âŸ§Ê¸} {y : âŸ¦ Ïƒ âŸ§Ê¸} â†’ R Î± x y â†’ R Î± (f x) (g y)

 RË£ : {Î“ : Cxt} â†’ âŸ¦ â–¡ âŸ§Ê¸ â†’ âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£ â†’ âŸ¦ Î“ âŸ§Ë£ â†’ Set
 RË£ {Îµ}     _  _       _      = ğŸ™
 RË£ {Î“ â‚Š Ï} Î± (Î³ , a) (Î¾ , b) = RË£ Î± Î³ Î¾ Ã— R Î± a b

 --
 -- The Kleisli extension preserves R.
 --
 R[KEá¶¥] : (Ï : Ty) (Î± : âŸ¦ â–¡ âŸ§Ê¸) {f : â„• â†’ âŸ¦ âŸ¨ Ï âŸ©áµ‡ âŸ§Ê¸} {g : â„• â†’ âŸ¦ Ï âŸ§Ê¸}
        â†’ ((i : â„•) â†’ R Î± (f i) (g i))
        â†’ {Î“ : Cxt} {Î³ : âŸ¦ Î“ âŸ§Ë£}
        â†’ R Î± (âŸ¦ KEá¶¥ Ï âŸ§áµ Î³ f) g
 R[KEá¶¥]  Î¹      Î± Î¶ Ï‡ = trans (Î¶ _) (ap _ Ï‡)
 R[KEá¶¥] (_ â‡¾ Ï) Î± Î¶ Ï‡ = Î» Î· â†’ R[KEá¶¥] Ï Î± (Î» i â†’ Î¶ i Î·) Ï‡

 --
 -- Variables in related contexts are related.
 --
 R[Var] : {Î“ : Cxt} {Ï : Ty} {Î³ : âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£} {Î¾ : âŸ¦ Î“ âŸ§Ë£} (Î± : âŸ¦ â–¡ âŸ§Ê¸)
        â†’ RË£ Î± Î³ Î¾ â†’ (i : âˆ¥ Ï âˆˆ Î“ âˆ¥) â†’ R Î± (Î³ â‚ âŸ¨ i âŸ©áµ› â‚) (Î¾ â‚ i â‚)
 R[Var] {Îµ}     Î±  _      ()
 R[Var] {Î“ â‚Š Ï} Î± (_ , Î¸)  zero    = Î¸
 R[Var] {Î“ â‚Š Ï} Î± (Î¶ , _) (succ i) = R[Var] Î± Î¶ i

 --
 -- Terms are related to their b-translations.
 --
 Lemma[R] : {Î“ : Cxt} {Ï : Ty} (t : T Î“ Ï) (Î± : âŸ¦ â–¡ âŸ§Ê¸)
          â†’ {Î³ : âŸ¦ âŸª Î“ âŸ«áµ‡ âŸ§Ë£} {Î¾ : âŸ¦ Î“ âŸ§Ë£} â†’ RË£ Î± Î³ Î¾
          â†’ R Î± (âŸ¦ t áµ‡ âŸ§áµ Î³) (âŸ¦ t âŸ§áµ Î¾)
 Lemma[R] (Var i)  Î± Î¶ = R[Var] Î± Î¶ i
 Lemma[R] (Lam t)  Î± Î¶ = Î» Ï‡ â†’ Lemma[R] t Î± (Î¶ , Ï‡)
 Lemma[R] (f Â· a)  Î± Î¶ = Lemma[R] f Î± Î¶ (Lemma[R] a Î± Î¶)
 Lemma[R] Zero     _ _ = refl
 Lemma[R] Succ     Î± _ = ap succ
 Lemma[R] (Rec{Ï}) Î± _ {x} {y} Ï‡ {f} {g} Î· = R[KEá¶¥] Ï Î± claim
  where
   claim : (i : â„•) â†’ R Î± (rec x (Î» k â†’ f (Î» _ â†’ k)) i) (rec y g i)
   claim  0       = Ï‡
   claim (succ i) = Î· refl (claim i)

\end{code}

â—¼ Take â–¡ â‰¡ Î¹ â‡¾ Î¹

Because our goal is to prove continuity of functions (â„• â†’ â„•) â†’ â„•,
here we take â–¡ â‰¡ Î¹ â‡¾ Î¹.

\begin{code}

open Translation (Î¹ â‡¾ Î¹) public

--
-- a T-definable generic element â„•áµ‡ â†’ â„•áµ‡
--
Î© : {Î“ : Cxt} â†’ T Î“ (âŸ¨ Î¹ â‡¾ Î¹ âŸ©áµ‡) -- ((â„• â†’ â„•) â†’ â„•) â†’ (â„• â†’ â„•) â†’ â„•
Î© = Lam (Lam (Î½â‚€ Â· (Î½â‚ Â· Î½â‚€)))   -- Î»fÎ±.Î±(fÎ±)

--
-- Theorem: Any f : (â„• â†’ â„•) â†’ â„• is pointwise equal to fáµ‡(Î©).
--
Theorem[R] : (t : T Îµ ((Î¹ â‡¾ Î¹) â‡¾ Î¹))
           â†’ (Î± : â„•á´º) â†’ âŸ¦ t áµ‡ Â· Î© âŸ§ Î± â‰¡ âŸ¦ t âŸ§ Î±
Theorem[R] t Î± = Lemma[R] t Î± â‹† (ap Î±)

\end{code}
